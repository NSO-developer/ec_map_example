[doc Using --check-callbacks for the trafo value examples]

[include test-common.luxinc]

[shell genet_cli]
    [invoke genet_cli_initial]
    !no example-domain
    !commit
    ?Commit complete|No modifications to commit
[shell callbacks]
    !ncs_cmd -o -u admin -mc 'mdel /example-device/operational'
    !echo deleted: $$?
    ?deleted: 0
    # check callbacks for empty configuration; the argument is a path or namespace
    [invoke check-callbacks /example-domain]

    # now populate some parts of the data model
    !ncs_load -O -l -m < keyless.xml
    !echo loaded: $$?
    ?loaded: 0
[shell genet_cli]
    """!
    example-domain direct leaf-string a abcd
    example-domain direct leaf-empty a
    example-domain direct container-presence
    example-domain direct choice-in-choice h i 10.1.2.3
    example-domain value constant a 4711
    example-domain value composition a 0
    example-domain value types lists a 10
    example-domain value representation identityref a example-identity-id1
    example-domain types leaf-list a [ 2 3 4 ]
    """
    !commit
    ?Commit complete

    # check again (in this case using the namespace is equivalent to the top-level node)
    [invoke check-callbacks http://cisco.com/ns/genet/examples/transforms]


[cleanup]
    [invoke genet_cli]
    !config
    !no example-domain
    !commit
    ?Commit complete|No modifications to commit
    !exit
    !exit
    ?SH-PROMPT
    !ncs_cmd -o -u admin -mc 'mdel /example-device/operational'
    !echo deleted: $$?
    ?deleted: 0
